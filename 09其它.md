# 解析 JSON

```v
import json

struct Foo {
	x int
}

struct User {
	name string
	age  int
	// `skip` 属性跳过 JSON 解析
	foo Foo [skip]
	// 如果字段名不同于 JSON，需要特别声明
	last_name string [json: lastName]
}

data := '{ "name": "Frodo", "lastName": "Baggins", "age": 25 }'
user := json.decode(User, data) or {
	eprintln('Failed to decode json')
	return
}
println(user.name)
println(user.last_name)
println(user.age)
// 可以解析 JSON 数组
sfoos := '[{"x":123},{"x":456}]'
foos := json.decode([]Foo, sfoos) ?
println(foos[0].x)
println(foos[1].x)
```

因为 JSON 无处不在，V 直接支持。

`json.decode` 函数需要两个参数：一是 JSON 解析的类型，二是包含 JSON 数据的字符串。

V 会生成 JSON 编码和解析的代码，不是通过反射实现，因此性能更好。

# 测试

## 断言

```v
fn foo(mut v []int) {
	v[0] = 1
}

mut v := [20]
foo(mut v)
assert v[0] < 4
```

使用 assert 关键字来断言后面的表达式是否成立，如果表达式返回 false，则程序会停止，并将表达式两侧的值都打印到错误输出中。assert 多用于检测程序的错误，易于查找未预计产生的值，可以用于任何函数中。

## 测试文件

```v
// hello.v
module main

fn hello() string {
	return 'Hello world'
}

fn main() {
	println(hello())
}
```

```v
module main

// hello_test.v
fn test_hello() {
	assert hello() == 'Hello world'
}
```

执行 `v hello_test.v` 会运行 `test_hello()` 函数，检查 `hello()` 是否返回正确的结果。这个命令会执行 `hello_test.v` 中所有的测试函数。

- 测试函数必须放在以 `_test.v` 结尾的测试文件中
- 测试函数必须以 `test_` 开头，才能执行
- 普通函数和其它类型都能在测试文件中声明，并正常使用
- 有两种测试文件：外部的和内部的
- 内部的测试文件模块名与要测试的模块名一致，并且可以调用私有的函数和类型
- 外部的测试文件则需要 import 引入要测试的模块，只能调用公开的 API

上述例子中，`hello_test.v` 和 `hello.v` 都是 `main` 模块的，所以是内部测试文件。

在测试文件中可以定义特殊的测试函数：

- `testsuite_begin` 在运行所有测试函数前调用
- `testsuite_end` 在运行所有测试函数后调用

如果测试函数中有错误返回，任何错误传递都会使测试失败：

```v
import strconv

fn test_atoi() ? {
	assert strconv.atoi('1') ? == 1
	assert strconv.atoi('one') ? == 1 // 测试失败
}
```

## 运行测试

运行单个测试文件 `v foo_test.v`，运行这个模块的测试 `v test mymodule`，或者 `v test .` 这会运行当前文件夹包其子文件夹下所有测试文件。使用 `--stats` 可以各个测试单元的详细信息。

可以创建一个名为 `testdata` 的文件夹存放额外的特殊数据（包括 .v 源文件），紧邻 \_test.v 文件。V 的测试框架在运行测试时会忽略此类文件夹。想放置带有无效 V 源代码或其他测试（包括已知失败的测试）的 .v 文件时就很有用，这些测试由 parent \_test.v 文件以特定方式/选项运行。

V 编译器的路径可由 @EXE 获取，所有在一个测试文件中，可以直接运行另一个测试文件：

```v
import os

fn test_subtest() {
	res := os.execute('${@VEXE} other_test.v')
	assert res.exit_code == 1
	assert res.output.contains('other_test.v does not exist')
}
```

# 内存管理

V 在一开始会使用值类型和字符串缓存来避免不必要的内存开销，提倡一种简单不过度抽象的代码风格。

大多数对象内存（约 90% - 100%）都由 V 的自动释放引擎释放。编译器在编译时会自动插入必要的内存释放调用，仍有一小部分通过引用计数释放。

开发者不需要改变任何代码，依旧有效，像 Python，Go 或 Java，除了没有影响效率的垃圾回收器进行每个对象的追踪和回收。

## 控制

可以利用 V 的自动释放引擎，为自定义类型定义 free 函数：

```v
struct MyType {}

[unsafe]
fn (data &MyType) free() {
	// ...
}
```

如编译器使用 C 的 free 函数来释放 C 的数据类型，编译器会在自定义数据类型的变量生命周期结束时静态插入 free 函数调用。

开发者如果想要更多的底层控制，可以使用 `-manualfree` 关闭自动释放引擎，或者在想要手动控制的函数上加 `[manualfree]` 属性。

注意：自动释放引擎在 V 0.3 版本后才会自动生效，此前需要使用 -autofree 手动使用。如果不使用的话，V 程序会产生内存泄漏。

## 例子

```v
import strings

fn draw_text(s string, x int, y int) {
	// ...
}

fn draw_scene() {
	// ...
	name1 := 'abc'
	name2 := 'def ghi'
	draw_text('hello $name1', 10, 10)
	draw_text('hello $name2', 100, 10)
	draw_text(strings.repeat(`X`, 10000), 10, 50)
	// ...
}
```

这些字符串不会从 draw_text 函数中逃逸，所以会在函数调用结束后回收。

实际上，使用 `-prealloc` 的话，前两个调用不会产生内存分配，这个两个字符串很小，V 会使用预分配缓存存放。

```v
struct User {
	name string
}

fn test() []int {
	number := 7 // 栈中分配变量
	user := User{} // 栈中分配结构体
	numbers := [1, 2, 3] // 数字分配在堆上，在函数结束时被释放
	println(number)
	println(user)
	println(numbers)
	numbers2 := [4, 5, 6] // 数组会返回，因此不在这里释放
	return numbers2
}
```

## 堆栈

堆栈基本：

如其它编程语言类似，有两个地方用来存放数据：

- 栈，能够以几乎零开销的方式快速进行分配。栈的增减随着函数的调用深度变化，每个函数都有自己的栈调用空间，直到函数返回前都是有效的。没有释放的对象也是必要的，然而这个对象当函数返回是时就无效了。此外，栈空间是有限的，通常每个线程只有几 M。
- 堆，由操作系统管理的大内存区域（通常几 G）。堆对象的分配和释放委派给操作系统的特殊函数调用来实现。这就意味着这些对象在多个函数调用中仍然有效，但是相应的管理成本就比较高。

### V 默认的方式

出于性能考虑，V 尽可能将对象分配在栈上，但是明显必须的话，也会分配到堆上。

例如：

```v
struct MyStruct {
	n int
}

struct RefStruct {
	r &MyStruct
}

fn main() {
	q, w := f()
	println('q: $q.r.n, w: $w.n')
}

fn f() (RefStruct, &MyStruct) {
	a := MyStruct{
		n: 1
	}
	b := MyStruct{
		n: 2
	}
	c := MyStruct{
		n: 3
	}
	e := RefStruct{
		r: &b
	}
	x := a.n + c.n
	println('x: $x')
	return e, &c
}
```

因为 a 从来没有离开 f 函数，a 是分配在栈上的。然而 b 的引用是作为 c 的一部分返回，以及直接返回 c 的引用，因此，b 和 c 是分配在堆上的。

当一个对象的引用做为函数的参数传入，那边这个堆栈分配就变得不是那边清晰：

```v
struct MyStruct {
mut:
	n int
}

fn main() {
	mut q := MyStruct{
		n: 7
	}
	w := MyStruct{
		n: 13
	}
	x := q.f(&w) // `q` 和 `w` 的引用传入
	println('q: $q\nx: $x')
}

fn (mut a MyStruct) f(b &MyStruct) int {
	a.n += b.n
	x := a.n * b.n
	return x
}
```

这里 q.f(&w) 的调用，将引用作为参数传入。由于 a 是 mut 的，b 是引用，所以理论上，会在 main 函数逃逸。然而，它们的生命周期没有在 main 函数内。所以 q 和 w 会分配在栈上。

### 手动控制堆栈

在上一个例子中，V 编译器将 q，w 放在栈上，是因为在调用时能推断这些应用的值只是读取或修改，而不是传递到其它地方。可以理解为将 q，w 借给 f 函数。

但是 f 函数对引用自身做操作的话，事情就不一样了：

```v
struct RefStruct {
mut:
	r &MyStruct
}

[heap]
struct MyStruct {
	n int
}

fn main() {
	m := MyStruct{}
	mut r := RefStruct{
		r: &m
	}
	r.g()
	println('r: $r')
}

fn (mut r RefStruct) g() {
	s := MyStruct{
		n: 7
	}
	r.f(&s) // 引用 `s` 设置到 `r` 中会传递回 `main() `
}

fn (mut r RefStruct) f(s &MyStruct) {
	r.r = s // 如果没有 `[heap]` 会触发错误
}
```

这里的 f 函数做了一个错误的事情就是将 s 的引用传递给 r。因为 s 的生命周期在 g 函数中，将其引用给到 r，会导致 r 引用了一个栈上的数据，编译器会给以警告。这就使得在 g 函数中调用 r.f(&s)，只是借用 s 的引用是错误的。

处理这种困境的办法是在声明 struct MyStruct 的时候在其上设置 [heap] 属性。这会使得编译器在分配 MyStruct 的对象时都设置在堆中，这样即使 g 返回了 s 的引用依旧是有效的。编译器会考虑到 MyStruct 的对象都是分配到堆中，检查 f 函数时会允许 s 的引用赋值给 r.r。

在其他语言中会常看到这种模式：

```v
fn (mut a MyStruct) f() &MyStruct {
	// a 的业务逻辑
	return &a // 返回 a 的引用
}
```

这里 f 函数将 a 的引用既做为接收器又做为函数返回值，这里的目的是可以实现向这样：y = x.f().g() 的链式调用。然而这种方式的问题，会创建第二个 a 的引用，不仅仅是借用，这就使得 MyStruct 不得不声明 [heap] 属性。

在 V 中更好的方式是：

```v
struct MyStruct {
mut:
	n int
}

fn (mut a MyStruct) f() {
	// `a` 的业务逻辑
}

fn (mut a MyStruct) g() {
	// `a` 的其它业务逻辑
}

fn main() {
	x := MyStruct{} // 栈分配
	mut y := x
	y.f()
	y.g()
	// 替代 `mut y := x.f().g()
}
```

这样 [heap] 属性就可以避免，执行效率更好。

然而，如上文提到栈空间是很有限的，因此一些比较大的结构体更适合使用 [heap] 属性声明，将其分配在堆中，即使不是上述所要求的情况。

有另一种方式可以根据情况手动控制分配，虽然不推荐但还是展示在这里：

```v
struct MyStruct {
	n int
}

struct RefStruct {
mut:
	r &MyStruct
}

// 在 `g()` 调用之后覆盖栈空间
fn use_stack() {
	x := 7.5
	y := 3.25
	z := x + y
	println('$x $y $z')
}

fn main() {
	m := MyStruct{}
	mut r := RefStruct{
		r: &m
	}
	r.g()
	use_stack() // 覆盖栈空间
	println('r: $r')
}

fn (mut r RefStruct) g() {
	s := &MyStruct{ // `s` 显式引用堆对象
		n: 7
	}
	// 改变 `&MyStruct` -> `MyStruct`，`r.f(s)` -> `r.f(&s)`
	// 为了确定栈空间数据被重写
	r.f(s)
}

fn (mut r RefStruct) f(s &MyStruct) {
	r.r = unsafe { s } // 忽略编译器检查
}
```

unsafe 块会禁止编译器检查，使用 &MyStruct{...} 的声明方式，使得 s 即使没有使用 [heap] 属性也被分配到堆上。

最后一步编译器不是必须的，但是没有的话，引用中 r 会是非法的，指向的内存区域被 use_stack 函数重写了，程序会崩溃，或至少产生无法预料的输出。这就是为什么该方法是不安全的，应避免使用。

# ORM

（仍然在 alpha 阶段）

V 自身支持 ORM（对象关系映射），支持 SQLite，MySQL 和 Postgres，后续也会支持 MS SQL 和 Oracle。

V 的 ORM 有如下优势：

- 对所有 SQL 都采用统一的语法，迁移数据库时更加方便。
- 查询语句使用 V 语法构造，无需学习新的语法。
- 安全，所有查询语句自动检查，防止 SQL 注入。
- 编译时检查，避免在运行时才发现拼写错误。
- 简单易理解，无需手动解析查询结果，构造对象。

```v
import sqlite

struct Customer {
	// 目前结构体名就是表名
	id        int    [primary; sql: serial] // 自增 `id` 必须是第一个
	name      string [nonull]
	nr_orders int
	country   string [nonull]
}

db := sqlite.connect('customers.db') ?

// 创建表
// CREATE TABLE IF NOT EXISTS `Customer` (`id` INTEGER PRIMARY KEY, `name` TEXT NOT NULL, `nr_orders` INTEGER, `country` TEXT NOT NULL)
sql db {
	create table Customer
}

// select count(*) from Customer
nr_customers := sql db {
	select count from Customer
}
println('number of all customers: $nr_customers')
// V 语法可以用来构建查询语句
uk_customers := sql db {
	select from Customer where country == 'uk' && nr_orders > 0
}
println(uk_customers.len)
for customer in uk_customers {
	println('$customer.id - $customer.name')
}
// 添加 `limit 1` 只返回一个结果
customer := sql db {
	select from Customer where id == 1 limit 1
}
println('$customer.id - $customer.name')
// 插入一个新的 customer
new_customer := Customer{
	name: 'Bob'
	nr_orders: 10
}
sql db {
	insert new_customer into Customer
}
```

更多例子和文档参见：[vlib/orm](https://github.com/vlang/v/tree/master/vlib/orm)。

# 文档编写

文档编写方式和 Go 类似，十分简单。无需在代码之外单独写文档，vdoc 会从代码中的文档字符串生成文档。

每个函数、类型、常量的文档放置在声明之前：

```v
// clearall clears all bits in the array
fn clearall() {
}
```

内容必须已定义的名字开始。

有时一行文字不够表达，可以分成多行表示：

```v
// copy_all recursively copies all elements of the array by their value,
// if `dupes` is false all duplicate values are eliminated in the process.
fn copy_all(dupes bool) {
	// ...
}
```

为了便于理解，文档使用现在时语态。

一个模块的介绍必须放在第一个行，紧接着模块名。

生成文档使用 vdoc，例如：`v doc net.http`。

# 工具

## 代码格式化 v fmt

无需担心如何格式化代码或设置代码样式手则，使用 v fmt 即可：

```sh
v fmt file.v
```

推荐设置到编辑器中，每次保存时执行 v fmt -w。一次格式化消耗短（< 30ms）。建议在推送代码前都运行一下。

## 性能

V 能很好的支持对程序的性能测试：v -profile profile.txt run file.v，会生成一个 profile.txt 文件便于分析。

生成的 profile.txt 文件有 4 列：

a) 函数调用频次

b) 函数调用总共耗时（ms）

c) 函数调用平均耗时（ns）

d) 函数名

对第 3 列（每个函数平均调用时间）排序：sort -n -k3 profile.txt|tail

也可以使用 stopwatches 测试部分代码：

```v
import time

fn main() {
	sw := time.new_stopwatch()
	println('Hello world')
	println('Greeting the world took: ${sw.elapsed().nanoseconds()}ns')
}
```
