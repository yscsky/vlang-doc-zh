# 类型声明

## 接口

```v
struct Dog {
	breed string
}

struct Cat {
	breed string
}

fn (d Dog) speak() string {
	return 'woof'
}

fn (c Cat) speak() string {
	return 'meow'
}

// 不同于 Go 但类似于 TypeScript, V 的接口除了方法还可以定义字段
interface Speaker {
	breed string
	speak() string
}

dog := Dog{'Leonberger'}
cat := Cat{'Siamese'}

mut arr := []Speaker{}
arr << dog
arr << cat
for item in arr {
	println('a $item.breed says: $item.speak()')
}
```

一个类型实现一个接口，只要实现接口的方法和字段，无需特别声明这个实现关系，也就是没有 "implements" 关键字。

### 转换接口

使用动态转换符 `is` 可以测试一个接口底层实际类型是什么：

```v
interface Something {}

fn announce(s Something) {
	if s is Dog {
		println('a $s.breed dog') // `s` 自动转换为 `Dog`
	} else if s is Cat {
		println('a $s.breed cat')
	} else {
		println('something else')
	}
}
```

更多参见[动态转换](#动态转换)。

### 接口方法定义

接口也能实现方法，也就是一个接口可以实现另一个接口的方法。当一个结构体被包装成一个接口，两者都实现相同的接口方法。此时接口变量调用的是接口实现的方法：

```v
struct Cat {}

fn (c Cat) speak() string {
	return 'meow!'
}

interface Adoptable {}

fn (a Adoptable) speak() string {
	return 'adopt me!'
}

fn new_adoptable() Adoptable {
	return Cat{}
}

fn main() {
	cat := Cat{}
	assert cat.speak() == 'meow!'
	a := new_adoptable() // 此时 a 的类型是 Adoptable
	assert a.speak() == 'adopt me!'
	if a is Cat { // 此时 a 的类型转换成 Cat
		println(a.speak()) // meow!
	}
}
```

## 函数类型

使用类型别名来命名一个函数签名，例如：

```v
type Filter = fn (string) string
```

这样就和类型一样，函数可以使用函数类型作为参数：

```v
type Filter = fn (string) string

fn filter(s string, f Filter) string {
	return f(s)
}
```

函数声明时无需特别定义，只要签名相同就可以使用：

```v
fn uppercase(s string) string {
	return s.to_upper()
}

// `uppercase` 可以传入任何使用 Filter 的地方
```

也可以明确定义函数类型：

```v
my_filter := Filter(uppercase)
```

这个转换纯粹是为了方便检查，其实可以无需特别声明：

```v
my_filter := uppercase
```

可以传递一个赋值的函数变量作为参数：

```v
println(filter('Hello world', my_filter)) // 打印 `HELLO WORLD`
```

也可以直接传递函数无需声明本地变量：

```v
println(filter('Hello world', uppercase))
```

匿名函数也可以：

```v
println(filter('Hello world', fn (s string) string {
	return s.to_upper()
}))
```

完整[例子](demos/function/types.v)。

## 枚举

```v
enum Color {
	red
	green
	blue
}

mut color := Color.red
// V 知道 `color` 是 `Color` 枚举类型， 无需使用 `color = Color.green`
color = .green
println(color) // "green"
match color {
	.red { println('the color was red') }
	.green { println('the color was green') }
	.blue { println('the color was blue') }
}
```

在 match 中使用枚举要么完全列举，要么使用 else，确保有新枚举字段添加时，能完全覆盖到。

枚举字段不能使用保留关键字，如果使用前面需要加 @。

```v
enum Color {
	@none
	red
	green
	blue
}

color := Color.@none
println(color)
```

整数可以赋值到枚举字段中：

```v
enum Grocery {
	apple
	orange = 5
	pear
}

g1 := int(Grocery.apple)
g2 := int(Grocery.orange)
g3 := int(Grocery.pear)
println('Grocery IDs: $g1, $g2, $g3') // Grocery IDs: 0, 5, 6
```

枚举值不能进行计算操作，操作的话需要转换成整型。

## 联合类型

联合类型的实例可以表示多个类型中的一个。使用 type 关键字定义：

```v
struct Moon {}

struct Mars {}

struct Venus {}

type World = Mars | Moon | Venus

sum := World(Moon{})
assert sum.type_name() == 'Moon'
println(sum)
```

内建函数 `type_name` 返回当前表示的类型名字。

利用联合类型可以构建简洁有效的递归结构代码：

```v
// V 二叉树
struct Empty {}

struct Node {
	value f64
	left  Tree
	right Tree
}

type Tree = Empty | Node

// 节点值求和
fn sum(tree Tree) f64 {
	return match tree {
		Empty { 0 }
		Node { tree.value + sum(tree.left) + sum(tree.right) }
	}
}

fn main() {
	left := Node{0.2, Empty{}, Empty{}}
	right := Node{0.3, Empty{}, Node{0.4, Empty{}, Empty{}}}
	tree := Node{0.5, left, right}
	println(sum(tree)) // 0.2 + 0.3 + 0.4 + 0.5 = 1.4
}
```

枚举也可以定义方法，和结构体一样：

```v
enum Cycle {
	one
	two
	three
}

fn (c Cycle) next() Cycle {
	match c {
		.one {
			return .two
		}
		.two {
			return .three
		}
		.three {
			return .one
		}
	}
}

mut c := Cycle.one
for _ in 0 .. 10 {
	println(c)
	c = c.next()
}
```

输出：

```
one
two
three
one
two
three
one
two
three
one
```

### 动态转换

检查一个联合类型的实例是否持有特定类型，使用 `sum is Type`。转换为特定类型，使用 `sum as Type`：

```v
struct Moon {}

struct Mars {}

struct Venus {}

type World = Mars | Moon | Venus

fn (m Mars) dust_storm() bool {
	return true
}

fn main() {
	mut w := World(Moon{})
	assert w is Moon
	w = Mars{}
	// 使用 `as` 转换成 Mars 实例
	mars := w as Mars
	if mars.dust_storm() {
		println('bad weather!')
	}
}
```

as 转换时如果不是 Mars 类型实例，会产生 panic。所以更安全的方式是使用智能转换。

### 智能转换

```v
if w is Mars {
	assert typeof(w).name == 'Mars'
	if w.dust_storm() {
		println('bad weather!')
	}
}
```

在 if 中 w 的类型就是 Mars，这就是 _flow-sensitive typing_。如果 w 是可变变量，需要在 w 前添加 mut，告诉编译器：

```v
if mut w is Mars {
	assert typeof(w).name == 'Mars'
	if w.dust_storm() {
		println('bad weather!')
	}
}
```

否则 w 保持原来的类型不变。对于复杂表达式 user.name 同样适用。

### 匹配联合类型

使用 match 可以确定类型：

```v
struct Moon {}

struct Mars {}

struct Venus {}

type World = Mars | Moon | Venus

fn open_parachutes(n int) {
	println(n)
}

fn land(w World) {
	match w {
		Moon {}
		Mars {
			open_parachutes(3)
		}
		Venus {
			open_parachutes(1)
		}
	}
}
```

match 必须列举所有可能类型或者使用 else 分支：

```v
struct Moon {}
struct Mars {}
struct Venus {}

type World = Moon | Mars | Venus

fn (m Moon) moon_walk() {}
fn (m Mars) shiver() {}
fn (v Venus) sweat() {}

fn pass_time(w World) {
    match w {
        // w 在各个分之中会自动转换
        Moon { w.moon_walk() }
        Mars { w.shiver() }
        else {}
    }
}
```

## 类型别名

使用别名的方式定义一个新类型，例如：`type NewType = ExistingType`。

这是联合类型的一种特殊形式。

## 选项/结果类型和错误处理

选项类型使用 `?Type` 来声明：

```v
struct User {
	id   int
	name string
}

struct Repo {
	users []User
}

fn (r Repo) find_user_by_id(id int) ?User {
	for user in r.users {
		if user.id == id {
			// V 自动包装成选择类型
			return user
		}
	}
	return error('User $id not found')
}

fn main() {
	repo := Repo{
		users: [User{1, 'Andrew'}, User{2, 'Bob'}, User{10, 'Charles'}]
	}
	user := repo.find_user_by_id(10) or { // 选项类型必须使用 `or` 块处理
		return
	}
	println(user.id) // "10"
	println(user.name) // "Charles"
}
```

V 结合了选项和结果到同一个类型中，无需决定使用哪一个。

将一个函数升级为选项函数，只需在返回结果前中加 `?`，如果发生错误就返回错误类型。

如果不需要返回错误，则直接 `return none`，这比 `return error("")` 要高效。

这是 V 的主要错误处理机制，其仍是返回值，但是优势在于错误不能不被处理，而且处理的方式咩有那么繁琐。不同于其它语言，没有 `throw/try/catch`。

```v
user := repo.find_user_by_id(7) or {
	println(err) // "User 7 not found"
	return
}
```

### 处理选项

有 4 中处理选项的方式。

第一种，直接传递给上层：

```v
import net.http

fn f(url string) ?string {
	resp := http.get(url) ?
	return resp.text
}
```

因为 `http.get` 返回 `?http.Response`，f 中没有处理而是直接传递给调用 f 的函数，所有 f 函数返回值也要加上 `?`。如果错误传递到 main 函数仍然没有处理，会发生 panic。

上诉 f 函数本质上是：

```v
    resp := http.get(url) or { return err }
    return resp.text
```

第二种，提前中断执行：

```v
user := repo.find_user_by_id(7) or { return }
```

这里，你要么调用 `panic()` 或 `exit()`，结束整个程序。要么使用控制流表达式（`return`, `break`, `continue`, 等）结束当前代码块。注意，`break` 和`continue`只能用在 for 循环中。

V 不能强制分解选项（例如 Rust 中的 `unwarp()` 或 Swift 的 `!` ）。而是使用 `or { panic(err.msg) }` 替代。

第三种，在 or 块中提供默认值，如果发送错误，会将这个默认值赋予变量，这个默认值值必须和变量是相同类型。

```v
fn do_something(s string) ?string {
	if s == 'foo' {
		return 'foo'
	}
	return error('invalid string') // `return none` 也行
}

a := do_something('foo') or { 'default' } // a 会是 'foo'
b := do_something('bar') or { 'default' } // b 会是 'default'
println(a)
println(b)
```

第四种，使用 if 分解：

```v
import net.http

if resp := http.get('https://google.com') {
	println(resp.text) // resp 是 http.Response 而不是选项类型
} else {
	println(err)
}
```

上述代码中，resp 只能在 if 的第一个分支中，err 只能在 else 分支中。

# 泛型

```v
struct Repo<T> {
    db DB
}

struct User {
	id   int
	name string
}

struct Post {
	id   int
	user_id int
	title string
	body string
}

fn new_repo<T>(db DB) Repo<T> {
    return Repo<T>{db: db}
}

// 泛型函数，V 会为每个使用的类型生成函数
fn (r Repo<T>) find_by_id(id int) ?T {
    table_name := T.name // 在这个例子中获取类型的名字就是获取表名
    return r.db.query_one<T>('select * from $table_name where id = ?', id)
}

db := new_db()
users_repo := new_repo<User>(db) // 返回 Repo<User>
posts_repo := new_repo<Post>(db) // 返回 Repo<Post>
user := users_repo.find_by_id(1)? // find_by_id<User>
post := posts_repo.find_by_id(1)? // find_by_id<Post>
```

当前泛型函数定义必须声明它们的类型参数，但将来 V 将从运行时参数类型中的单字母类型名称推断泛型类型参数。这就是 `find_by_id` 可以省略 `<T>` 的原因，因为接收器参数 r 使用泛型类型 T。

另一个例子：

```v
fn compare<T>(a T, b T) int {
	if a < b {
		return -1
	}
	if a > b {
		return 1
	}
	return 0
}

// compare<int>
println(compare(1, 0)) // Outputs: 1
println(compare(1, 1)) //          0
println(compare(1, 2)) //         -1
// compare<string>
println(compare('1', '0')) // Outputs: 1
println(compare('1', '1')) //          0
println(compare('1', '2')) //         -1
// compare<f64>
println(compare(1.1, 1.0)) // Outputs: 1
println(compare(1.1, 1.1)) //          0
println(compare(1.1, 1.2)) //         -1
```
